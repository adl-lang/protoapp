module protoapp.apis.ui {

import common.http.Unit;
import common.http.HttpPost;
import common.http.HttpGet;
import common.time.Instant;
import common.strings.StringNE;
import common.strings.StringML;
import common.strings.Password;

import protoapp.db.AppUserId;
import protoapp.db.MessageId;

struct ApiRequests {

  /// AWS default compatible health check
  HttpGet<Unit> healthy = {
    "path": "/",
    "security" : "public"
  };


  /// Test the server is live
  HttpPost<Unit, Unit> ping = {
    "path": "/ping",
    "security" : "public"
  };

  /// Login a user
  ///
  /// The response will set an httpOnly cookie containing the refresh token
  HttpPost<LoginReq, LoginResp> login = {
    "path": "/login",
    "security" : "public"
  };

  /// Get a refreshed access token
  ///
  /// If the refresh token is not provided in the request body, then it will
  /// be read from the refrestToken cookie in the request.
  HttpPost<RefreshReq, RefreshResp> refresh = {
    "path": "/refresh",
    "security" : "public"
  };

  /// Clear the `refreshToken` cookie.
  HttpPost<Unit, Unit> logout = {
    "path": "/logout",
    "security" : "public"
  };

  /// Post a message to the noticeboard
  HttpPost<NewMessageReq, MessageId> newMessage = {
    "path": "/messages/new",
    "security" : "token"
  };

  /// Get recent noticeboard messages
  HttpPost<RecentMessagesReq, Paginated<Message> > recentMessages = {
    "path": "/messages/recent",
    "security" : "token"
  };

  /// Gets the logged in user details
  // NOTE: Fails with 401 if the token is invalid or user does not exist
  HttpGet<UserProfile> whoAmI = {
    "path": "/whoami",
    "security" : "token"
  };
};

struct LoginReq {
  StringNE email;
  Password password;
};

union LoginResp {
  LoginTokens tokens;
  Void invalid_credentials;
};

struct RefreshReq {
  Nullable<StringNE> refresh_token = null;
};

union RefreshResp {
  StringNE access_token;
  Void invalid_refresh_token;
};

struct LoginTokens {
  StringNE access_jwt;
  StringNE refresh_jwt;
};

struct NewMessageReq {
  StringML message;
};

struct RecentMessagesReq {
  // Pagination offset
  Word32 offset = 0;

  // Max number of items to return
  Word32 limit = 20;
};

struct Message {
  MessageId id;
  Instant posted_at;
  String user_fullname;
  StringML message;
};

struct UserProfile {
  AppUserId id;
  String fullname;
  String email;
  Bool is_admin;
};

/// A holder for paginated results
struct Paginated<T> {

  /// The paginated items
  Vector<T> items;

  /// The offset used for this query
  Word32 current_offset;

  /// The size of the entire date set
  Word32 total_count;
};

};
