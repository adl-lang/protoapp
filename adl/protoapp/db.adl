module protoapp.db {

import common.time.Instant;
import common.db.DbTable;
import common.db.DbKey;
import common.db.WithId;
import common.strings.StringNE;
import common.strings.StringML;

struct AppUser {
  StringNE fullname;
  StringNE email;
  Bool is_admin;
  StringNE hashed_password = "";
};

newtype AppUserTable = WithId<AppUser>;
type AppUserId = DbKey<AppUserTable>;

annotation AppUserTable DbTable {
  "indexes" : [["email"]],
  "uniqueness_constraints": [["email"]],
  "extra_sql": [
     "grant select on app_user to admin_user;"
  ]
};

struct Message {
  Instant posted_at;
  AppUserId posted_by;
  StringML message;

};
newtype MessageTable = WithId<Message>;
type MessageId = DbKey<MessageTable>;

annotation MessageTable DbTable {
  "indexes" : [["posted_at"]],
  "extra_sql": [
     "grant all on message to admin_user;"
  ]
};

};
